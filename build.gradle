import org.springframework.security.crypto.password.StandardPasswordEncoder

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE"
        classpath "org.springframework.boot:spring-boot-starter-security:1.3.2.RELEASE"
    }
}

plugins {
    id 'com.github.ksoichiro.web.resource' version '1.7.0'
}

def vJavaLang = '1.8'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.github.ksoichiro.web.resource'

repositories {
    jcenter()
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.2'
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4"
    compile "org.thymeleaf.extras:thymeleaf-extras-conditionalcomments"
    compile "com.h2database:h2"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.security:spring-security-test:4.0.3.RELEASE"
}

jar {
    baseName = 'sbac'
    version = '0.0.1-SNAPSHOT'
}

webResource {
    base {
        dest = 'src/main/resources/static'
    }
    bower {
        dependencies {
            install name: 'jquery', version: '1.11.2', filter: ["dist/*.min.*"]
            install name: 'bootstrap', version: '3.3.4', filter: ["dist/css/*.min.css", "dist/js/*.min.js", "dist/fonts/*"]
            install name: 'components-font-awesome', version: '4.3.0', filter: ["css/*.min.css", "fonts/*"]
            install name: 'respond-minmax', version: '1.4.2', filter: ["dest/*.min.js"]
            install name: 'html5shiv', version: '3.7.2', filter: ["dist/*.min.js"]
        }
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("${buildDir}/classes/main")

        excludeDirs += [
                file('src/main/resources/static/css'),
                file('src/main/resources/static/js'),
                file('src/main/resources/static/lib'),
                file('bower_components'),
                file('node_modules')
        ]
    }
    project {
        languageLevel = vJavaLang
        ipr {
            withXml { provider ->
                def mapping = provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping
                mapping.@vcs = 'Git'
                mapping.@directory = '$PROJECT_DIR$'
            }
        }
    }
}

// e.g. ./gradlew encodePassword -Pargs="test"
task encodePassword << {
    StandardPasswordEncoder encoder = new StandardPasswordEncoder()
    String username = 'test'
    if (project.hasProperty('args')) {
        username = project.args.split('\\s+')[0]
    }
    println "Encoded password for user ${username}:"
    println encoder.encode(username)
}

apply from: 'name-generator.gradle'

compileJava.dependsOn 'webResourceCompile'
task stage
stage.dependsOn 'assemble'
